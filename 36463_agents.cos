

* Event scripts
**
** Controller scripts
**

scrp 1 1 36463 9
	tick rand 500 2000
	doif sean eq 0
		doif name "spring" eq 1
			gsub spawn
		else
			gsub hide
		endi
	elif sean eq 1
		doif name "summer" eq 1
			gsub spawn
		else
			gsub hide
		endi
	elif sean eq 2
		doif name "fall" eq 1
			gsub spawn
		else
			gsub hide
		endi
	else
		doif name "winter" eq 1
			gsub spawn
		else
			gsub hide
		endi
	endi

	subr spawn
		alph 100 1
		show 1

		setv va00 ov00
		doif va00 eq 3
			setv va01 0
		elif va00 eq 8
			setv va01 1
		elif va00 eq 11
			setv va01 2
		endi

		setv va02 posl
		setv va03 post

		new: simp 2 va00 36463 "betaship_agents" 1 va01 rand 1500 4000
		mesg wrt+ targ 1000 va02 va03 1
		velo rand -15 15 -1
	retn

	subr hide
		alph 100 0
		show 0
	retn
endm

**
** Creation scripts
**

** Seed creation
scrp 2 3 36463 1000
* Attr: mouseable 2 + collide 64 + physics 128 = 194
	attr 194
* Bhvr: eat 16 + pick up 32
	bhvr 48

* Set physics attributes
	accg 2
	aero 3

* Set collision attributes
	perm 60
	fric 60
	elas 30

	emit 7 0.3

* Move to the spot if possible
	doif tmvt _p1_ _p2_ ne 0
		mvto _p1_ _p2_
	else
		kill targ
	endi

	tick 1
endm

** Fruit creation
scrp 2 8 36463 1000
* Attr: mouseable 2 + collide 64 + physics 128 = 194
	attr 194
* Bhvr: eat 16 + pick up 32
	bhvr 48

* Set physics attributes
	accg 2
	aero 3

* Set collision attributes
	perm 60
	fric 60
	elas 30

	emit 6 0.3
* Move to the spot if possible
	doif tmvt _p1_ _p2_ ne 0
		mvto _p1_ _p2_
	else
		kill targ
	endi
	tick 1
endm

** Food creation
scrp 2 11 36463 1000
* Attr: mouseable 2 + collide 64 + physics 128 = 194
	attr 194
* Bhvr: eat 16 + pick up 32
	bhvr 48

* Set physics attributes
	accg 2
	aero 3

* Set collision attributes
	perm 60
	fric 60
	elas 30

	emit 8 0.3

* Move to the spot if possible
	doif tmvt _p1_ _p2_ ne 0
		mvto _p1_ _p2_
	else
		kill targ
	endi
	tick 1
endm

** Plant creation
scrp 2 4 36463 1000
* Attr: mouseable 2 + collide 64 + physics 128 = 194
	attr 194
* Bhvr: act1 1
	bhvr 1
* Set physics attributes
	accg 2
	aero 3
* Set collision attributes
	perm 100
	fric 100
	elas 0
* emit flower smell
	emit 9 0.3
* Move to the spot if possible
	doif tmvt _p1_ _p2_ ne 0
		mvto _p1_ _p2_
	else
		kill targ
	endi
	tick 1
endm

** Flower creation
scrp 2 7 36463 1000
* Attr: mouseable 2 + collide 64 + physics 128 = 194
	attr 194
* Bhvr: act1 1
	bhvr 1
* Set physics attributes
	accg 2
	aero 3
* Set collision attributes
	perm 100
	fric 100
	elas 0
* emit flower smell
	emit 9 0.3
* Move to the spot if possible
	doif tmvt _p1_ _p2_ ne 0
		mvto _p1_ _p2_
	else
		kill targ
	endi
	tick 1
endm

** Bug creation
scrp 2 13 36463 1000
* Attr: mouseable 2 + collide 64 + physics 128 = 194
	attr 194
* Bhvr: act1 1 + hit 8 + eat 16 + pick up 32 = 57
	bhvr 57
* Set physics attributes
	accg 2
	aero 3
* Set collision attributes
	perm 60
	fric 60
	elas 30
* Move to the spot if possible
	doif tmvt _p1_ _p2_ ne 0
		mvto _p1_ _p2_
	else
		kill targ
	endi
	tick 1
endm

** Critter creation
scrp 2 15 36463 1000
* Attr: mouseable 2 + collide 64 + physics 128 = 194
	attr 194
* Bhvr: act1 1 + hit 8 + eat 16 + pick up 32 = 57
	bhvr 57
* Set physics attributes
	accg 2
	aero 3
* Set collision attributes
	perm 60
	fric 60
	elas 30
* Move to the spot if possible
	doif tmvt _p1_ _p2_ ne 0
		mvto _p1_ _p2_
	else
		kill targ
	endi
	tick 1
endm

** Toy creation
scrp 2 21 36463 1000
* Attr: mouseable 2 + activateable 4 + collide 64 + physics 128 = 198
	attr 198
* Bhvr: act1 1 + hit 8 + pick up 32 = 41
	bhvr 41
* Set physics attributes
	accg 2
	aero 3
* Set collision attributes
	perm 60
	fric 60
	elas 60
* Move to the spot if possible
	doif tmvt _p1_ _p2_ ne 0
		mvto _p1_ _p2_
	else
		kill targ
	endi
endm


**
** Event scripts
**

** Activate 1 scripts
* Toy play
scrp 2 21 36463 1
	stim writ from 97 2
	velo rand -3 3 -3
endm
* Plant play
scrp 2 4 36463 1
	stim writ from 84 1
	setv va00 velx
	mulv va00 3
	velo va00 -3
endm
* Flower play
scrp 2 7 36463 1
	stim writ from 84 1
	setv va00 velx
	mulv va00 3
	velo va00 -3
endm
* Bug play
scrp 2 13 36463 1
	stim writ from 85 1
	setv va00 velx
	mulv va00 3
	velo va00 -3
endm
* Critter play
scrp 2 15 36463 1
	stim writ from 86 1
	setv va00 velx
	mulv va00 3
	velo va00 -3
endm

** Hit scripts
* Toy hit
scrp 2 21 36463 3
	velo rand -3 3 rand -6 -3
endm
* Bug hit
scrp 2 13 36463 3
	stim writ from 87 1
	velo rand -3 3 rand -6 -3
endm
* Critter hit
scrp 2 15 36463 3
	stim writ from 87 1
	velo rand -3 3 rand -6 -3
endm

** Eat scripts
* Seed eat
scrp 2 3 36463 12
	stim writ from 77 2
	kill ownr
endm
* Fruit eat
scrp 2 8 36463 12
	stim writ from 78 2
	kill ownr
endm
* Food eat
scrp 2 11 36463 12
	stim writ from 79 2
	kill ownr
endm
* Bug eat
scrp 2 13 36463 12
	stim writ from 80 2
	kill ownr
endm
* Critter eat
scrp 2 15 36463 12
	stim writ from 80 2
	kill ownr
endm


**
** Timer scripts
**

* Seed
scrp 2 3 36463 9
* If more than 3 in the vicinity, kill this one
	setv va00 0
	rnge 400
	esee 2 3 36463
		addv va00 1
	next
	doif va00 gt 3
		kill ownr
		stop
	endi
	tick rand 200 400
	addv ov00 1
	doif ov00 gt 10
		kill ownr
		stop
	endi
endm
* Fruit
scrp 2 8 36463 9
* If more than 3 in the vicinity, kill this one
	setv va00 0
	rnge 400
	esee 2 8 36463
		addv va00 1
	next
	doif va00 gt 3
		kill ownr
		stop
	endi
	tick rand 200 400
	addv ov00 1
	doif ov00 gt 10
		kill ownr
		stop
	endi
endm
* Food
scrp 2 11 36463 9
* If more than 3 in the vicinity, kill this one
	setv va00 0
	rnge 400
	esee 2 11 36463
		addv va00 1
	next
	doif va00 gt 3
		kill ownr
		stop
	endi
	tick rand 200 400
	addv ov00 1
	doif ov00 gt 10
		kill ownr
		stop
	endi
endm

** Plant
scrp 2 4 36463 9
	tick rand 500 900
	setv va00 grap posx posy
	setv va01 prop va00 2
* get the heat CA
	doif va01 gt 0.60 and va01 lt 0.71
		alph 0 0
		doif rand 0 3 eq 3
			setv va02 posl
			setv va03 post
			new: simp 2 8 36463 "betaship_agents" 1 1 rand 1200 4000
			mesg wrt+ targ 1000 va02 va03 1
			velo rand -5 5 -5
		endi
	else
		alph 100 1
	endi
endm
** Flower
scrp 2 7 36463 9
	tick rand 500 900
	setv va00 grap posx posy
	setv va01 prop va00 2
* get the heat CA
	doif va01 gt 0.45 and va01 lt 0.60
		alph 0 0
		doif rand 0 3 eq 3
			setv va02 posl
			setv va03 post
			new: simp 2 3 36463 "betaship_agents" 1 0 rand 1200 4000
			mesg wrt+ targ 1000 va02 va03 1
			velo rand -5 5 -5
		endi
	else
		alph 100 1
	endi
endm



* Bug
scrp 2 13 36463 9
	doif carr eq null
		setv va00 rand -8 8
		doif va00 lt 0
			setv va00 rand -8 -5
			pose 1
		elif va00 gt 0
			setv va00 rand 5 8
			pose 0
		endi
		reps rand 4 12
			doif fall ne 1
				setv va20 grap posx posy
				doif rtyp va20 eq 8 or rtyp va20 eq 9
					accg 0.8
				elif rtyp va20 eq -1
					kill targ
					stop
				else
					accg 2
				endi
				velo va00 -8
			endi
			wait 15
		repe
		doif rand 0 5 eq 0
			rnge 1000
			setv va00 rand 0 3
			esee 2 13 36463
				addv va00 1
			next
			doif va00 lt 4
* Create new bugs
				setv va01 posx
				setv va02 post
				subv va02 5
				reps 2
					new: simp 2 13 36463 "betaship_agents" 2 6 rand 1200 4000
					mesg wrt+ targ 1000 va01 va02 0
				repe
			elif va00 gt 7
				kill ownr
			endi
		endi
	endi
	tick rand 50 85
endm

* Critter
scrp 2 15 36463 9
	doif carr eq null
		setv va00 rand -8 8
		doif va00 lt 0
			setv va00 rand -8 -5
			pose 1
		elif va00 gt 0
			setv va00 rand 5 8
			pose 0
		endi
		reps rand 4 12
			doif fall ne 1
				setv va20 grap posx posy
				doif rtyp va20 eq 8 or rtyp va20 eq 9
					accg 0.8
				elif rtyp va20 eq -1
					kill targ
					stop
				else
					accg 2
				endi
				velo va00 -8

			endi
			wait 15
		repe
		doif rand 0 5 eq 0
			rnge 1000
			setv va00 rand 0 3
			esee 2 15 36463
				addv va00 1
			next
			doif va00 lt 4
* Create new critters
				setv va01 posx
				setv va02 post
				subv va02 5
				reps 2
					new: simp 2 15 36463 "betaship_agents" 2 4 rand 1200 4000
					mesg wrt+ targ 1000 va01 va02 0
				repe
			elif va00 gt 7
				kill ownr
			endi
		endi
	endi
	tick rand 50 85
endm
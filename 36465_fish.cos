** Aqua biome fish spawn
reps 8
	new: simp 2 15 36465 "betaship_fish" 2 0 rand 1500 4000
	mesg wrt+ targ 1000 47680 28434 1
	velo rand -100 100 0
repe

** Big ocean fish spawns
reps 8
	new: simp 2 15 36465 "betaship_fish" 2 0 rand 1500 4000
	mesg wrt+ targ 1000 45262 27782 1
	velo rand -100 100 0
repe
reps 8
	new: simp 2 15 36465 "betaship_fish" 2 0 rand 1500 4000
	mesg wrt+ targ 1000 46832 26281 1
	velo rand -100 100 0
repe
reps 8
	new: simp 2 15 36465 "betaship_fish" 2 0 rand 1500 4000
	mesg wrt+ targ 1000 49832 26281 1
	velo rand -100 100 0
repe
reps 8
	new: simp 2 15 36465 "betaship_fish" 2 0 rand 1500 4000
	mesg wrt+ targ 1000 51268 27798 1
	velo rand -100 100 0
repe

** Fish creation
scrp 2 15 36465 1000
* Attr: mouseable 2 + collide 64 + physics 128 = 194
	attr 194
* Bhvr: act1 1 + hit 8 + eat 16 + pick up 32 = 57
	bhvr 57
* Set physics attributes
	accg 0
	aero 3
* Set collision attributes
	perm 5
	fric 60
	elas 30
* Move to the spot if possible
	doif tmvt _p1_ _p2_ ne 0
		mvto _p1_ _p2_
	else
		kill targ
	endi
	tick 1
endm

** Fish timer
scrp 2 15 36465 9
	doif carr eq null
		setv va00 rand -9 9
		doif va00 lt 0
			setv va00 rand -3 -2
			setv va01 rand -1 1
			pose 1
		elif va00 gt 0
			setv va00 rand 2 3
			setv va01 rand -1 1
			pose 0
		endi
		reps rand 4 12
			setv va02 rtyp grap posx posy
			doif va02 eq 8 or va02 eq 9
				velo va00 va01
			else
				velo 0 10
			endi
			wait 15
		repe
		doif rand 0 5 eq 0

			setv va02 rtyp grap posx posy
			doif va02 eq 8 or va02 eq 9
			else
			kill ownr
			endi

			rnge 700
			setv va00 rand 0 3
			esee 2 15 36465
				addv va00 1
			next
			doif va00 lt 4
* Create new fish
				setv va01 posx
				setv va02 post
				subv va02 5
				reps 2
					new: simp 2 15 36465 "betaship_fish" 2 0 rand 1200 4000
					mesg wrt+ targ 1000 va01 va02 0
				repe
			elif va00 gt 8
				kill ownr
			endi
		endi
	endi
	tick rand 50 85
endm


* Fish play
scrp 2 15 36465 1
	stim writ from 86 1
	setv va00 velx
	mulv va00 3
	velo va00 -3
endm
* Fish hit
scrp 2 15 36465 3
	stim writ from 87 1
	velo rand -3 3 rand -6 -3
endm
* Fish eat
scrp 2 15 36465 12
	stim writ from 80 2
	kill ownr
endm

rscr
enum 2 15 36465
	kill targ
next
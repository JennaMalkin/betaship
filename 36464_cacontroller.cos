
**
** Caemitter controller
**
**

new: simp 1 22 36464 "blnk" 1 0 0
tick 600

**
** Plant and flower connecting to CA 9
**

*Plants
cacl 2 4 0 9
*Flowers
cacl 2 7 0 9


** Timer script

scrp 1 22 36464 9
	enum 1 1 36464
		setv va00 ov01
		doif ov00 eq 1
* Light dependant of time of day and season
			doif time eq 0
				mulv va00 0.2
			elif time eq 1
				mulv va00 0.60
			elif time eq 2
				mulv va00 1.2
				doif sean eq 1
					mulv va00 1.2
				elif sean eq 3
					mulv va00 0.93
				endi
			elif time eq 3
				mulv va00 0.27
			elif time eq 4
				mulv va00 0
			endi
		elif ov00 eq 2
* Heat dependant of season and time of day
			doif time eq 2
				mulv va00 1.3
			elif time eq 4
				mulv va00 0.7
			endi

			doif sean eq 0 or sean eq 2
				mulv va00 1
			elif sean eq 1
				mulv va00 2
			elif sean eq 3
				mulv va00 0.30
			endi
		endi
		emit ov00 va00
	next
endm

rscr
enum 1 22 36464
	kill targ
next